/*
 * Copyright (c) 2017 Amit Kumar.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed
 * under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * Set up Checkstyle, Findbugs and PMD to perform extensive code analysis.
 *
 * Gradle tasks added:
 * - checkstyle
 * - pmd
 *
 * The two tasks above are added as dependencies of the check task so running check will
 * run all of them.
 */

apply plugin: 'checkstyle'
apply plugin: 'pmd'

apply from: "$rootDir/script/utils.gradle"

dependencies {
    checkstyle 'com.puppycrawl.tools:checkstyle:7.7'
}

ext.qualityConfigDir = "$rootDir/script/quality";
ext.reportsDir = "$buildDir/reports"

check.dependsOn 'checkQuality'

class CheckQuality extends DefaultTask {}

task checkQuality(type: CheckQuality) {
    group = "Verification"
    description = "Runs all static analysis checks on all build variants."
}

def generateCheckQualityTask(String buildVariant, boolean withLint) {
    buildVariant = buildVariant.capitalize()

    def checkQualityVariant = "check${buildVariant}Quality"
    def checkQualityVariantTaskDependencies = [
            "checkstyle${buildVariant}",
            "pmd${buildVariant}"
    ]
    if (withLint) {
        checkQualityVariantTaskDependencies += "lint${buildVariant}"
    }

    task(checkQualityVariant, type: CheckQuality, dependsOn: checkQualityVariantTaskDependencies) {
        group = "Verification"
        description = "Runs all static analysis checks on the ${buildVariant} build variant."
    }

    tasks.checkQuality.dependsOn "${checkQualityVariant}"
}

def generateCheckQualityTaskForVariant(variant) {
    generateCheckQualityTask(variant.name, true)
}

removeCheckTaskIf("checkstyle", Checkstyle)

task checkstyle(type: Checkstyle) {
    group = "Verification"
    description = "Checks style of source code of all build variants."
}

def generateCheckstyleTask(String flavor, String buildType, String buildVariant) {
    def checkstyleVariant = "checkstyle${buildVariant.capitalize()}"

    task(checkstyleVariant, type: Checkstyle) {
        group = "Verification"
        description = "Checks style of source code for ${buildVariant} build variant."

        configFile file("$qualityConfigDir/checkstyle/checkstyle-config.xml")
        configProperties.checkstyleSuppressionsPath = file("$qualityConfigDir/checkstyle/suppressions.xml").absolutePath
        configProperties.checkstyleJavaHeaderPath = file("$qualityConfigDir/checkstyle/java.header").absolutePath
        configProperties.checkstyleXmlHeaderPath = file("$qualityConfigDir/checkstyle/xml.header").absolutePath

        source resolveSourceDirs(flavor, buildType, buildVariant)
        classpath = files() // intentionally blank to prevent duplicate analysis
        include '**/*.java'
        include '**/*.xml'
        exclude '**/gen/**'

        reports {
            xml.enabled = false
            html.enabled = true
            xml {
                destination "$reportsDir/checkstyle/${buildVariant}/${checkstyleVariant}.xml"
            }
            html {
                destination "$reportsDir/checkstyle/${buildVariant}/${checkstyleVariant}.html"
            }
        }
    }

    tasks.checkstyle.dependsOn checkstyleVariant
}

def generateCheckstyleTaskForVariant(variant) {
    def flavor = variant.flavorName
    def buildType = variant.buildType.name
    def buildVariant = flavor + (flavor.isEmpty() ? buildType : buildType.capitalize())
    generateCheckstyleTask(flavor, buildType, buildVariant)
}

removeCheckTaskIf("pmd", Pmd)

task pmd(type: Pmd) {
    group = "Verification"
    description = "Finds bugs in source code on all build variants."
}

def generatePMDTask(String flavor, String buildType, String buildVariant) {
    def pmdVariant = "pmd${buildVariant.capitalize()}"

    task(pmdVariant, type: Pmd) {
        group = "Verification"
        description = "Finds bugs in source code on the {$buildVariant} build variant."

        ignoreFailures = false
        ruleSetFiles = files("$qualityConfigDir/pmd/pmd-ruleset.xml")
        ruleSets = []

        source resolveSourceDirs(flavor, buildType, buildVariant)
        include '**/*.java'
        exclude '**/gen/**'

        reports {
            xml.enabled = false
            html.enabled = true
            xml {
                destination "$reportsDir/pmd/${buildVariant}/${pmdVariant}.xml"
            }
            html {
                destination "$reportsDir/pmd/${buildVariant}/${pmdVariant}.html"
            }
        }
    }

    tasks.pmd.dependsOn pmdVariant
}

def generatePMDTaskForVariant(variant) {
    def flavor = variant.flavorName
    def buildType = variant.buildType.name
    def buildVariant = flavor + (flavor.isEmpty() ? buildType : buildType.capitalize())
    generatePMDTask(flavor, buildType, buildVariant)
}



ext {
    generateCheckQualityTask = this.&generateCheckQualityTask
    generateCheckQualityTaskForVariant = this.&generateCheckQualityTaskForVariant
    generateCheckstyleTask = this.&generateCheckstyleTask
    generateCheckstyleTaskForVariant = this.&generateCheckstyleTaskForVariant
    generatePMDTask = this.&generatePMDTask
    generatePMDTaskForVariant = this.&generatePMDTaskForVariant
}

